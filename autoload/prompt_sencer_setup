function prompt_namaz_yaz {
  [[ -n $TMUX ]] && exit 0
  [[ $NAMAZ -lt 0 ]] && exit
  kalan=$[NAMAZ-`date +%s`]
  echo "[$[kalan/3600]sa$[(kalan%3600)/60]dk]"
}

# set color of the prompt according the mode
# might need adding zle-line-init
function zle-keymap-select {
  if [[ $KEYMAP == "main" ]]; then
    if [[ $ZLE_STATE == *insert* ]]; then
      PROMPTCOLOR=73
    else
      PROMPTCOLOR=226
    fi
  else
    PROMPTCOLOR=164
  fi
  zle reset-prompt
}

zle -N zle-keymap-select

# do not go one char back when going into cmd mode

function vi-cmd-mode {
  zle .vi-cmd-mode
  let CURSOR++
}

zle -N vi-cmd-mode

# reset color before starting new line

function zle-line-finish {
  PROMPTCOLOR=73
  zle reset-prompt
}

zle -N zle-line-finish

function prompt_sencer_setup {
  prompt_opts=(cr percent subst)
  PROMPTCOLOR=73
  export PROMPT="%F{\$PROMPTCOLOR}%h:%F{174}%C%f "
  export RPROMPT="%F{\$PROMPTCOLOR}\$(prompt_namaz_yaz)[\${PWD/${HOME//\//\\/}/~}]%f"
}

prompt_sencer_setup "$@"

# vim: ft=zsh
