#!/bin/bash

awk '$1 ~/ibrav/ {
       ibrav = $3
     }
     $1 ~ /celldm/ {
       a = $3 * 0.529177249
       switch(ibrav){
        case 1:
          b = a
          c = a
          break

        case 6:
          b = a
          getline
          c = a * $3
          break

        case 8:
          getline
          b = a * $3
          getline
          c = a * $3
          break

        default:
          print "Only ibrav 1, 6, and 8 are implemented"
       }
     }
     $1 ~ /nat/ {
       j = $3 + 0 # "+ 0" gets rid of "," at the end, if present
     }
     $1 ~ /ATOMIC_POSITIONS/ {
       print j
       printf "celldm %11.6f %11.6f %11.6f 90.0 90.0 90.0\n", a, b, c
       if(tolower($0) ~ /angstrom/) { a = 1; b = 1; c = 1 }
       else if(tolower($0) ~ /bohr/) { a = 0.529177249; b = 0.529177249; c = 0.529177249 }
       for(i = 1;i <= j;i++){
         getline
         printf "%-5s  %15.10f %15.10f %15.10f\n", $1, $2 * a, $3 * b, $4 * c
       }
     }' ${1:-input.txt}
